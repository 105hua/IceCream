plugins {
    id 'java'
    id 'com.diffplug.spotless' version '7.0.0.BETA2'
    id 'com.gradleup.shadow' version '8.3.1'
}

group = 'com.nearvanilla'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.1-R0.1-SNAPSHOT")
    implementation("org.incendo:cloud-paper:2.0.0-beta.10")
    implementation("org.incendo:cloud-annotations:2.0.0")
    annotationProcessor("org.incendo:cloud-processor:2.0.0")
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

spotless{
    format("misc"){
        target '**/*.gradle', '**/*.md'
        trimTrailingWhitespace()
        indentWithSpaces(4)
    }
    java{
        importOrder()
        cleanthat()
        googleJavaFormat() // Remove Unused Imports conflicts with Google Java Format.
        formatAnnotations()
        licenseHeader '/* Licensed under the GNU Affero General Public License. */'
    }
}

tasks.shadowJar{
    enableRelocation = true
    relocationPrefix = "${rootProject.property('group')}.${rootProject.property('name').toString().toLowerCase()}.lib"
    minimize()
    archiveClassifier.set("")
}

tasks.build{
    dependsOn("shadowJar")
}